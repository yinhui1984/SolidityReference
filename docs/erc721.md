---
key: ERC-721, NFT
desc: Non-Fungible Token Standard 非同质化代币
---

EIP : https://eips.ethereum.org/EIPS/eip-721
openzeppelin : https://docs.openzeppelin.com/contracts/3.x/erc721



## ERC-721 Non-Fungible Token Standard

https://eips.ethereum.org/EIPS/eip-721



 ### 简单摘要
一个用于不可伪造的代币(非同质化代币)的标准接口，也被称为契约。

 ### 摘要
以下标准允许在智能合约中实现NFTs的标准API。该标准提供了跟踪和转移NFT的基本功能。

我们考虑了NFTs由个人拥有和交易以及委托给第三方经纪商/钱包/拍卖商（"运营商"）的用例。NFTs可以代表对数字或实物资产的所有权。我们考虑了多种多样的资产，我们知道你会想出更多。

+ 有形财产--房屋、独特的艺术品

+ 虚拟收藏品--独特的小猫图片、可收藏的卡片

+ "负值 "资产--贷款、负担和其他责任

一般来说，所有的房子都是与众不同的，没有两只小猫是相同的。NFT是有区别的，你必须分别追踪每一个的所有权。



### 动机

一个标准的接口允许钱包/经纪商/拍卖应用程序与以太坊上的任何NFT一起工作。我们提供了简单的ERC-721智能合约以及跟踪任意数量的NFT的合约。其他的应用将在下面讨论。

这个标准受到ERC-20代币标准的启发，并建立在EIP-20创建以来两年的经验上。EIP-20不足以追踪NFT，因为每个资产都是不同的（不可替代的），而每个数量的代币是相同的（可替代的）。

该标准与EIP-20之间的差异将在下文中进行探讨。



 ### 规格

本文件中的关键词 "必须"、"必须不"、"必须"、"应该"、"不应该"、"建议"、"可能 "和 "可选 "应按照RFC 2119中的描述进行解释。

每个符合ERC-721标准的合约都必须实现ERC721和[ERC165](https://eips.ethereum.org/EIPS/eip-165)接口（受以下 "注意事项 "的限制）。



#### ERC721 接口

```solidity
pragma solidity ^0.4.20;

/// @title ERC-721 Non-Fungible Token Standard
/// @dev See https://eips.ethereum.org/EIPS/eip-721
///  Note: the ERC-165 identifier for this interface is 0x80ac58cd.
interface ERC721 /* is ERC165 
		*/ {
    /// @dev This emits when ownership of any NFT changes by any mechanism.
    ///  This event emits when NFTs are created (`from` == 0) and destroyed
    ///  (`to` == 0). Exception: during contract creation, any number of NFTs
    ///  may be created and assigned without emitting Transfer. At the time of
    ///  any transfer, the approved address for that NFT (if any) is reset to none.
    /// @dev 当任何NFT的所有权更改时（不管哪种方式），就会触发此事件。
    /// 在创建（此时`from` == 0）和销毁NFT时(此时`to` == 0)，也会会触发此事件。
    /// 创建合约时例外：此时可以创建和派发任意数量的NFT而无需触发此事件。
    /// 在进行任何转移时，该 NFT（如果有）的被授权地址将重置为无。
    event Transfer(address indexed _from, address indexed _to, uint256 indexed _tokenId);

    /// @dev This emits when the approved address for an NFT is changed or
    ///  reaffirmed. The zero address indicates there is no approved address.
    ///  When a Transfer event emits, this also indicates that the approved
    ///  address for that NFT (if any) is reset to none.
    /// @dev 当更改或确认NFT的被授权地址时触发。
    ///  零地址表示没有被授权的地址。
    ///  触发 `Transfer` 事件时，该NFT的被授权地址（如果有）同样被重置为“无”（零地址）。
    event Approval(address indexed _owner, address indexed _approved, uint256 indexed _tokenId);

    /// @dev This emits when an operator is enabled or disabled for an owner.
    ///  The operator can manage all NFTs of the owner.
    /// @dev 所有者启用或禁用操作员时触发。
    /// 操作员可管理所有者所持有的NFTs。
    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);

    /// @notice Count all NFTs assigned to an owner
    /// @dev NFTs assigned to the zero address are considered invalid, and this
    ///  function throws for queries about the zero address.
    /// @param _owner An address for whom to query the balance
    /// @return The number of NFTs owned by `_owner`, possibly zero
    /// @notice 统计所有者持有的NFTs数量
    /// @dev NFT 不能分配给零地址，查询零地址同样会异常
    /// @param _owner ： 查询余额的地址
    /// @return `_owner`所有的NFT数量，可以是0
    function balanceOf(address _owner) external view returns (uint256);

    /// @notice Find the owner of an NFT
    /// @dev NFTs assigned to zero address are considered invalid, and queries
    ///  about them do throw.
    /// @param _tokenId The identifier for an NFT
    /// @return The address of the owner of the NFT
    /// @notice 返回所有者
    /// @dev NFT分配给零地址是无效的，查询零地址抛出异常
    /// @param _tokenId NFT的编号（id），也是其识别码。
    /// @return 返回所有者地址
    function ownerOf(uint256 _tokenId) external view returns (address);

    /// @notice Transfers the ownership of an NFT from one address to another address
    /// @dev Throws unless `msg.sender` is the current owner, an authorized
    ///  operator, or the approved address for this NFT. Throws if `_from` is
    ///  not the current owner. Throws if `_to` is the zero address. Throws if
    ///  `_tokenId` is not a valid NFT. 
    //// When transfer is complete, this function
    ///  checks if `_to` is a smart contract (code size > 0). If so, it calls
    ///  `onERC721Received` on `_to` and throws if the return value is not
    ///  `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`.
    /// @param _from The current owner of the NFT
    /// @param _to The new owner
    /// @param _tokenId The NFT to transfer
    /// @param data Additional data with no specified format, sent in call to `_to`
    /// @notice 将NFT的所有权从一个地址转移到另一个地址
    /// @dev 如果`msg.sender` 不是当前的所有者（或被授权者）则抛出异常
    /// 如果 `_from` 不是所有者、`_to` 是零地址、`_tokenId` 不是有效id 均抛出异常。
    /// 当转移完成时，函数检查  `_to` 是否为合约（代码数量 > 0），如果是，调用 `_to`的 `onERC721Received` 并且检查返回值是否是 `0x150b7a02` 
    /// （即：`bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`）如果不是抛出异常。    /// @param _from ：当前的所有者
    /// @param _from NFT当前的所有者
    /// @param _to ：新的所有者
    /// @param _tokenId ：要转移的token id.
    /// @param data : 附加额外的参数（没有指定格式），传递给接收者。
    function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes data) external payable;

    /// @notice Transfers the ownership of an NFT from one address to another address
    /// @dev This works identically to the other function with an extra data parameter,
    ///  except this function just sets data to "".
    /// @param _from The current owner of the NFT
    /// @param _to The new owner
    /// @param _tokenId The NFT to transfer
    /// @notice 将NFT的所有权从一个地址转移到另一个地址，功能同上，不带data参数。
    function safeTransferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice Transfer ownership of an NFT -- THE CALLER IS RESPONSIBLE
    ///  TO CONFIRM THAT `_to` IS CAPABLE OF RECEIVING NFTS OR ELSE
    ///  THEY MAY BE PERMANENTLY LOST
    /// @dev Throws unless `msg.sender` is the current owner, an authorized
    ///  operator, or the approved address for this NFT. Throws if `_from` is
    ///  not the current owner. Throws if `_to` is the zero address. Throws if
    ///  `_tokenId` is not a valid NFT.
    /// @param _from The current owner of the NFT
    /// @param _to The new owner
    /// @param _tokenId The NFT to transfer
    /// @notice 转移所有权 -- 调用者负责确认`_to`是否能接收NFT，否则可能永久丢失。
    /// @dev 如果`msg.sender` 不是当前的所有者（或被授权操作员，或被授权地址）则抛出异常
    /// 如果 `_from` 不是当前的所有者、`_to` 是零地址、`_tokenId` 不是有效id 均抛出异常。
    function transferFrom(address _from, address _to, uint256 _tokenId) external payable;

    /// @notice Change or reaffirm the approved address for an NFT
    /// @dev The zero address indicates there is no approved address.
    ///  Throws unless `msg.sender` is the current NFT owner, or an authorized
    ///  operator of the current owner.
    /// @param _approved The new approved NFT controller
    /// @param _tokenId The NFT to approve
    /// @notice 更改或确认NFT的被授权地址
    /// @dev 零地址表示没有被授权的地址。
    ///  如果`msg.sender` 不是当前的所有者或被授权的操作员则抛出异常。
    /// @param _approved 新授权的控制者
    /// @param _tokenId ： 授予的token id
    function approve(address _approved, uint256 _tokenId) external payable;

    /// @notice Enable or disable approval for a third party ("operator") to manage
    ///  all of `msg.sender`'s assets
    /// @dev Emits the ApprovalForAll event. The contract MUST allow
    ///  multiple operators per owner.
    /// @param _operator Address to add to the set of authorized operators
    /// @param _approved True if the operator is approved, false to revoke approval
    /// @notice 启用或禁用授权第三方（操作员）管理 `msg.sender` 所有资产
    /// @dev 触发 ApprovalForAll 事件，合约必须允许每个所有者可以有多个操作员。
    /// @param _operator 要添加到授权操作员列表中的地址
    /// @param _approved True 表示操作员被授权, false 表示撤销授权
    function setApprovalForAll(address _operator, bool _approved) external;

    /// @notice Get the approved address for a single NFT
    /// @dev Throws if `_tokenId` is not a valid NFT.
    /// @param _tokenId The NFT to find the approved address for
    /// @return The approved address for this NFT, or the zero address if there is none
    /// @notice 获取单个NFT的授权地址
    /// @dev 如果 `_tokenId` 无效，抛出异常。
    /// @param _tokenId ：  token id
    /// @return 返回被授权地址， 零地址表示没有被授权地址。
    function getApproved(uint256 _tokenId) external view returns (address);

    /// @notice Query if an address is an authorized operator for another address
    /// @param _owner The address that owns the NFTs
    /// @param _operator The address that acts on behalf of the owner
    /// @return True if `_operator` is an approved operator for `_owner`, false otherwise
    /// @notice 查询一个地址是否是另一个地址的被授权操作员
    /// @param _owner 所有者
    /// @param _operator 代表所有者的被授权操作员
    /// @return 返回True则`_operator`是所有者的被授权操作员， 否则返回false。
    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
}

interface ERC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceID The interface identifier, as specified in ERC-165
    /// @dev Interface identification is specified in ERC-165. This function
    ///  uses less than 30,000 gas.
    /// @return `true` if the contract implements `interfaceID` and
    ///  `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```



#### 钱包接口

如果钱包/经纪人/拍卖应用程序接受安全转账，则它必须实现钱包接口。

```solidity
/// @dev Note: the ERC-165 identifier for this interface is 0x150b7a02.
interface ERC721TokenReceiver {
    /// @notice 处理接收NFT
    /// @dev ERC721智能合约在`transfer`完成后，在收币地址上调用这个函数。
    /// 函数可以复原或者拒绝转账。返回非`0x150b7a02` 也同样是导致交易被复原。
    /// 注意: 调用这个函数的 msg.sender是ERC721的合约地址
    /// @param _operator ：调用 `safeTransferFrom` 函数的地址。
    /// @param _from ：之前的NFT拥有者
    /// @param _tokenId ： NFT token id
    /// @param _data ： 附加信息
    /// @return 正确处理时返回 `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`
    function onERC721Received(address _operator, address _from, uint256 _tokenId, bytes _data) external returns(bytes4);
}
```

#### 元数据扩展

元数据扩展(metadata extension)对于ERC-721智能合约是可选的（见下面的 "注意事项"）。这允许你的智能合约被询问其名称和关于你的NFT所代表的资产的详细信息

```solidity
/// @title ERC-721 Non-Fungible Token Standard, optional metadata extension
/// @dev See https://eips.ethereum.org/EIPS/eip-721
///  Note: the ERC-165 identifier for this interface is 0x5b5e139f.
interface ERC721Metadata /* is ERC721 
		*/ {
    /// @notice 合约中NFT集合的名字
    function name() external view returns (string _name);

    /// @notice 其缩写名称
    function symbol() external view returns (string _symbol);

    /// @notice 一个给定资产的唯一的统一资源标识符（URI）。
    /// @dev 如果 `_tokenId` 无效，抛出异常。
    /// URIs在 RFC 3986 定义。ERC721 URI 可指向一个符合 "ERC721 URI JSON Schema" 的 JSON 文件。
    function tokenURI(uint256 _tokenId) external view returns (string);
}
```

这是上面提到的 "ERC721元数据JSON模式"。

```json
{
    "title": "Asset Metadata",
    "type": "object",
    "properties": {
        "name": {
            "type": "string",
            "description": "Identifies the asset to which this NFT represents"
        },
        "description": {
            "type": "string",
            "description": "Describes the asset to which this NFT represents"
        },
        "image": {
            "type": "string",
            "description": "A URI pointing to a resource with mime type image/* representing the asset to which this NFT represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive."
        }
    }
}
```

> 由于NFT最早面向艺术品，ERC-721可选Metadata接口“ERC721 Metadata JSON Schema”只指定了name、description、image三个属性（且不可修改）。但对于游戏而言，这些属性远远不够，因此我们下一节要介绍的另一个NFT标准EIP-1155将name、description、image属性转移到了URI的json里面，而不规定URI接口返回的JSON Schema
>
> ***问题在于 tokenURI 可指向一个JSON 文件，现有绝大多数NFT产品就通过将其图片或视频的URL记录到这个JSON 文件中，而完成其产品——这也就是说，这些所谓的“非同质化通证”的图片或视频大多保存在私有服务器上。这显然是有问题的，因为大家用智能合约玩的居然是互联网产品！！***



#### 枚举扩展

枚举扩展( enumeration extension)对于ERC-721智能合约是可选的（见下面的 "注意事项"）。这允许你的合约发布其完整的NFT列表，并使它们可以被发现。

```solidity
interface ERC721Enumerable {
    /// @notice  NFTs 计数
    /// @return  返回合约有效跟踪（所有者不为零地址）的 NFT数量
    function totalSupply() external view returns (uint256);

    /// @notice 枚举索引NFT
    /// @dev 如果 `_index` >= `totalSupply()` 则抛出异常
    /// @param _index 小于 `totalSupply()`的索引号
    /// @return 对应的token id（标准不指定排序方式)
    function tokenByIndex(uint256 _index) external view returns (uint256);

    /// @notice 枚举索引某个所有者的 NFTs
    /// @dev  如果 `_index` >= `balanceOf(_owner)` 或 `_owner` 是零地址，抛出异常
    /// @param _owner 查询的所有者地址
    /// @param _index 小于 `balanceOf(_owner)` 的索引号
    /// @return 对应的token id （标准不指定排序方式)
    function tokenOfOwnerByIndex(address _owner, uint256 _index) external view returns (uint256);}
```



### 注意事项

 `Solidity0.4.20`接口语法的表达能力不足以记录ERC-721标准。符合ERC-721的合约必须同时遵守以下内容。

+ Solidity issue #3412: 上述接口包括每个函数的明确可变性保证( explicit mutability guarantees )。可变性保证从弱到强依次是： `payable`, implicit nonpayable, `view`, and `pure`.。你的实现必须满足这个接口中的可变性保证，你也可以满足更强的保证。例如，这个接口中的一个`payable`的函数在你的合约中可能被实现为不可支付的(nonpayable)（没有指定状态可变性, (no state mutability specified) ）。我们期望以后的 Solidity 版本将允许你的更严格的合约继承自这个接口，但 0.4.20 版的一个变通方法是，你可以在继承你的合约之前编辑这个接口以添加更严格的可变性。
+ Solidity issue #3419: 实现 `ERC721Metadata` 或 `ERC721Enumerable` 的合约也必须实现 `ERC721`。`ERC-721`实现了接口`ERC-165`的要求。
+ Solidity issue #2330: 如果一个函数在本规范中被显示为外部函数(`external`)，那么如果它使用`public`可见性，合约将是合规的。作为0.4.20版本的变通方法，你可以在继承你的合约之前，编辑这个接口切换为`public`的。
+ Solidity issue #3494, #3544: 使用`this.*.selector` 被 Solidity 标记为警告，未来版本的 Solidity 不会将此标记为错误。

如果较新版本的 Solidity 允许在代码中表达这些警告，那么这个 EIP 可能会被更新，并删除这些警告，这将等同于原始规范。



### 基本原理

有许多提议的以太坊智能合约的使用取决于跟踪可区分的资产。现有的或计划中的NFTs的例子是Decentraland的LAND，CryptoPunks的同名小混混，以及使用DMarket或EnjinCoin等系统的游戏物品。未来的用途包括跟踪现实世界的资产，如房地产（如Ubitquity或Propy等公司所设想的）。在这些情况下，关键是这些项目不能作为数字 "混在一起"，而必须对每项资产的所有权进行单独和原子化的追踪。无论这些资产的性质如何，如果我们有一个标准化的接口，允许跨功能的资产管理和销售平台，那么这个生态系统将会更加强大。

#### **"NFT "一词的选择**

"NFT "几乎让所有接受调查的人都感到满意，并且广泛适用于可区分的数字资产的广泛领域。我们认识到，"契约 "对于本标准的某些应用（特别是物理财产）来说是非常具有描述性的。

考虑过的替代词：可区分的资产、所有权、代币、资产、股权、票据

#### **NFT标识符**

每个NFT都由ERC-721智能合约内唯一的uint256 ID来识别。这个识别号码在合约的有效期内不应该改变。这对`(contract address, uint256 tokenId)`将是以太坊链上特定资产的全球唯一和完全限定的标识符。虽然一些ERC-721智能合约可能会发现从ID 0开始，并为每个新的NFT简单地增加1，但调用者不应该认为ID号码有任何特定的模式，必须将ID视为一个 "黑盒子"。还要注意的是，NFT可能会变得无效（被销毁）。请参阅枚举函数以了解支持的枚举接口。

选择uint256允许多种应用，因为UUID和sha3哈希值可以直接转换为uint256。



#### 转让机制

ERC-721规范了一个安全传输函数`safeTransferFrom`（带或不带字节参数的重载）和一个不安全函数`transferFrom`。转移可以通过以下方式启动:

+ NFT所有者
+ NFT批准地址
+ 某一NFT当前所有者的授权操作者

此外，授权操作者可以为一个NFT设置批准的地址。这为钱包、经纪人和拍卖应用提供了一套强大的工具，以快速使用大量的NFT。

转移和接受函数的文档只指定了交易必须抛出(throw)的条件。你的实现也可以在其他情况下抛出。这允许实施者实现有趣的结果。

+ 如果合约暂停，不允许转让 - [prior art, CryptoKitties deployed contract, line 611](https://gist.github.com/arpit/071e54b95a81d13cb29681407680794f#file-cryptokitties-sol-L611)
+ 阻止某些地址接收NFTs-- [prior art, CryptoKitties deployed contract, lines 565, 566](https://gist.github.com/arpit/071e54b95a81d13cb29681407680794f#file-cryptokitties-sol-L565)
+ 不允许不安全的转移 - `transferFrom` 抛出，除非 `to` 等于` msg.sende` 或 `countOf(_to)` 非零或之前为非零（因为这种情况是安全的）
+ 向交易双方收取费用--如果之前是零地址，则在以非零`_approved`调用`approved`时要求付款；如果之前是非零地址，则在以零地址调用`approved`时要求退款；在调用任何转移函数时要求付款；要求转移参数`_to`等于`msg.sender`；要求转移参数`_to`是NFT的批准地址
+ 只读NFT注册表 - 总是从`safeTransferFrom`, `transferFrom`, `approve`和`setApprovalForAl`l中抛出。

失败的交易将被throw，这是ERC-223、ERC-677、ERC-827和OpenZeppelin对SafeERC20.sol的实现中确定的最佳做法。ERC-20定义了一个`allowance`功能(查询剩余额度)，这在调用后又修改为不同的金额时引起了问题，如OpenZeppelin问题#438。在ERC-721中，没有`allowance`，因为每个NFT都是唯一的，数量是无或一。因此，我们得到了ERC-20的原始设计的好处，没有后来发现的问题

创建NFT（"铸币",` minting`）和销毁NFT（`burning`）并不包括在规范中。您的合约可以通过其他方式实现这些。关于您在创建或销毁NFT时的责任，请参见`event`文件。



我们质疑`onERC721Received`上的`operator`参数是否有必要。在我们可以想象的所有情况下，如果operator很重要，那么该operator可以将令牌转移给他们自己，然后再发送--那么他们将是发送地址(`from`)。这似乎是故意的，因为我们认为operator是令牌的临时拥有者（而且转移到自己身上是多余的）。当operator发送代币时，是operator自己的行为，而不是operator代表代币持有人的行为。这就是为什么operator和之前的代币所有者对代币接收者都很重要。

考虑过的替代方案：只允许两步ERC-20风格的交易，要求转移函数永远不抛出，要求所有函数返回一个布尔值，表明操作的成功。



#### **ERC-165 Interface**

我们选择标准接口检测（ERC-165）来暴露ERC-721智能合约所支持的接口。

未来的EIP可能会创建一个全局合约的接口注册表。我们强烈支持这样的EIP，它将允许你的ERC-721实现通过委托给一个单独的合约来实现ERC721Enumerable、ERC721Metadata或其他接口。

####  gas和复杂度（关于枚举扩展）

本规范考虑了管理少量和任意大量NFT的实现。如果你的应用程序能够增长，那么在你的代码中**避免使用for/while循环**（见CryptoKitties悬赏问题#4）。这些表明你的合约可能无法扩展，gas成本将随着时间的推移无限制地上升。

我们已经在Testnet部署了一个合约，[XXXXERC721](https://github.com/fulldecent/erc721-example)，它实例化并跟踪340282366920938463463374607431768211456 个不同的合约（2^128）。这足以将每个IPV6地址分配给一个以太坊账户所有者，或者跟踪几微米大小的纳米机器人的所有权，总的来说是地球的一半。你可以从区块链上查询到它。而且每个功能都比查询ENS花费的gas少

这个例子明确指出：ERC-721标准的尺度。

考虑的替代方案：**如果资产枚举函数需要一个for-loop，就去掉它，从枚举函数返回一个Solidity数组类型**。



#### 隐私

在动机部分确定的钱包/经纪人/拍卖商有强烈的需求来确定所有者拥有哪些NFT。

考虑一个NFTs不能被列举的用例可能是有趣的，例如财产所有权的私人登记处，或部分私人登记处。然而，隐私是无法实现的，因为攻击者可以简单地（！）为每个可能的tokenId调用ownerOf (call `ownerOf` for every possible `tokenId`.)

#### 元数据选择（元数据扩展）

我们在元数据扩展中规定了名称和符号功能。我们审查的每个令牌EIP和草案（ERC-20、ERC-223、ERC-677、ERC-777、ERC-827）都包括这些函数。

我们提醒实现者，如果你抗议使用这种机制，空字符串是对名称和符号的有效回应。我们还提醒大家，任何智能合约都可以使用与你的合约相同的名称和符号。客户端如何确定哪些ERC-721智能合约是众所周知的（canonical），这超出了本标准的范围。

提供了一种机制来将NFT与URI联系起来。我们期望许多实现将利用这一点来为每个NFT提供元数据。图片大小的建议来自Instagram，他们可能对图片的可用性很了解。URI可能是可变的（即它会不时地改变）。我们考虑了一个代表房屋所有权的NFT，在这种情况下，关于房屋的元数据（图像、居住者等）可以自然改变。

**元数据被作为一个字符串值返回。目前这只能作为从web3的调用，不能从其他合约中调用**。这是可以接受的，因为我们还没有考虑到区块链上的应用程序会查询这些信息的用例。

考虑过的替代方案：把每个资产的所有元数据放在区块链上（太贵了），使用URL模板来查询元数据部分（URL模板不适合所有的URL方案，特别是P2P URL），多地址网络地址（不够成熟）。



#### 社区共识

在最初的ERC-721问题上发生了大量的讨论，此外，我们在Gitter上举行了第一次现场会议，有很好的代表性，并且得到了很好的宣传（在Reddit，在Gitter #ERC频道，以及最初的ERC-721问题）。感谢各位参与者。

@ImAAllInNow 来自DEC Gaming的Rob / 介绍2月7日密歇根州以太坊聚会的情况
@Arachnid Nick Johnson
@jadhavajay 来自AyanWorks的Ajay Jadhav
@superphly Cody Marx Bailey - XRAM Capital / 在黑客马拉松上分享 1月20日 / 联合国未来金融黑客马拉松。
@fulldecent William Entriken
在2018年的ETHDenver举行了第二次活动，讨论可区分的资产标准（笔记待公布）。

在这个过程中，我们的包容性很强，邀请任何有问题或有贡献的人加入我们的讨论。然而，这个标准的编写只是为了支持这里列出的已确定的用例。



####   向后兼容性

我们采用了ERC-20规范中的`balanceOf`、`totalSupply`、`name`和`symbol`语义。如果一个实现的目标是在支持这个标准的同时与ERC-20更加兼容，它也可以包括一个返回uint8(0)的函数decimals。然而，我们发现要求所有ERC-721的实现都支持小数函数是不切实际的。

CryptoKitties - 与本标准的早期版本兼容。
CryptoPunks - 部分兼容ERC-20，但不容易普及，因为它直接在合约中包括拍卖功能，并使用函数名称明确地将资产称为 "punks"。
Auctionhouse资产接口 - 作者需要一个Auctionhouse ÐApp（目前是冰盒）的通用接口。他的 "资产 "合约非常简单，但缺少ERC-20兼容性、approve()功能和元数据。这项工作在EIP-173的讨论中被提及。
注意：像Curio Cards和Rare Pepe这样的 "限量版、可收藏的代币 "并不是可区分的资产。它们实际上是单个可替换代币的集合，每个代币都由自己的智能合约跟踪，有自己的总供应量（在极端情况下可能是1）。

onERC721Received函数专门针对旧的部署的合约，这些合约在某些情况下可能会无意中返回1（真），即使它们没有实现一个函数（见Solidity DelegateCallReturnValue错误）。通过返回和检查一个神奇的值，我们能够区分实际的肯定性响应和这些空洞的真值。



### Test Cases

0xcert ERC-721 Token includes test cases written using Truffle.

###  Implementations

0xcert ERC721 – a reference implementation

- MIT licensed, so you can freely use it for your projects
- Includes test cases
- Active bug bounty, you will be paid if you find errors

Su Squares – an advertising platform where you can rent space and place images

- Complete the Su Squares Bug Bounty Program to seek problems with this standard or its implementation
- Implements the complete standard and all optional interfaces

ERC721ExampleDeed – an example implementation

- Implements using the OpenZeppelin project format

XXXXERC721, by William Entriken – a scalable example implementation

- Deployed on testnet with 1 billion assets and supporting all lookups with the metadata extension. This demonstrates that scaling is NOT a problem.

## 例子

加密猫 :  https://etherscan.io/address/0x06012c8cf97bead5deae237070f9587f8e7a266d#code

​					https://gist.github.com/arpit/071e54b95a81d13cb29681407680794f

​					https://www.cryptokitties.co/



Su Squares. [https://tenthousandsu.com](https://tenthousandsu.com/)

Decentraland. [https://decentraland.org](https://decentraland.org/)

CryptoPunks. https://www.larvalabs.com/cryptopunks

DMarket. [https://www.dmarket.io](https://www.dmarket.io/)

Enjin Coin. [https://enjincoin.io](https://enjincoin.io/)

Ubitquity. [https://www.ubitquity.io](https://www.ubitquity.io/)

Propy. [https://tokensale.propy.com](https://tokensale.propy.com/)

Su Squares Bug Bounty Program. https://github.com/fulldecent/su-squares-bounty

XXXXERC721. https://github.com/fulldecent/erc721-example

ERC721ExampleDeed. https://github.com/nastassiasachs/ERC721ExampleDeed

Curio Cards. [https://mycuriocards.com](https://mycuriocards.com/)





